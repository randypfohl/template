plugins {
    id("com.android.application")
    id("kotlin-android")
    id("kotlin-android-extensions")
    id("kotlin-kapt")
    id("androidx.navigation.safeargs")
}

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools

    bundle {
        density {
            enableSplit true
        }
        abi {
            enableSplit true
        }
        language {
            enableSplit true
        }
    }

    defaultConfig {
        applicationId config.applicationId
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk

        versionCode System.getenv("BUILD_VERSION_CODE")?.toInteger() ?: 1
        versionName System.getenv("BUILD_VERSION") ?: '1.0.0-dev'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary true
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("RELEASE_STORE") ?: "x")
            storePassword System.getenv("RELEASE_STORE_PASSWORD") ?: "x"
            keyAlias System.getenv("RELEASE_KEY_ALIAS") ?: "x"
            keyPassword System.getenv("RELEASE_KEY_PASSWORD") ?: "x"
        }
    }

    flavorDimensions "version"
    productFlavors {
        development {
            dimension "version"
            applicationId config.applicationId
            resConfigs("en", "xhdpi")
            aaptOptions.cruncherEnabled = false
            matchingFallbacks = ["full"]
        }
        full {
            dimension "version"
            applicationId config.applicationId
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable project.property("enableReleaseTesting") == "ON"
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
            manifestPlaceholders.appName = "${project.property("debugAppName")}"
        }
    }

    dataBinding {
        enabled = true
    }

    kotlinOptions {
        allWarningsAsErrors = true
    }

    testOptions {
        animationsDisabled = true
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation deps.koin.core
    implementation deps.koin.android
    implementation deps.koin.test
    implementation deps.koin.viewmodel

    implementation deps.kotlin.core
    implementation deps.kotlin.coroutinesCore
    implementation deps.kotlin.coroutinesAndroid

    implementation deps.support.core_ktx
    implementation deps.support.navigation_fragment
    implementation deps.support.navigation_ui_ktx
    implementation deps.support.fragment_runtime
    implementation deps.support.fragment_runtime_ktx
    implementation deps.support.preference

    if (project.property("enableReleaseTesting") == "ON") {
        implementation deps.support.fragment_testing
    } else {
        debugImplementation  deps.support.fragment_testing
    }

    kapt deps.room.compiler
    implementation deps.room.kotlin
    implementation deps.room.runtime
    androidTestImplementation deps.room.testing

    implementation deps.retrofit.gson
    implementation deps.retrofit.okhttp_logging_interceptor
    implementation deps.retrofit.runtime
    androidTestImplementation deps.retrofit.mock

    implementation deps.android.activity_ktx
    implementation deps.android.constraint_layout
    implementation deps.android.play_core

    implementation deps.espresso.idling_resource
    androidTestImplementation deps.espresso.intents
    androidTestImplementation deps.espresso.contrib
    androidTestImplementation(deps.espresso.core)
    androidTestImplementation deps.espresso.web

    testImplementation deps.junit.junit
    androidTestImplementation deps.junit.ext
}
